mode_setup;

em#    := 10pt#;
cap#   := 7pt#;
thin#  := 1/3pt#;
thick# := 5/6pt#;
o#     := 1/5pt#;

q#     := 1/12em#;

define_pixels(em, cap);
define_blacker_pixels(thin, thick);
define_corrected_pixels(o);

curve_sidebar = round 1/18em;

%%%%%%%%%%%%%%%%%%%% ROOK %%%%%%%%%%%%%%%%%%%%
def rook_outline = 
    numeric q;
    q = round 1/12em;

    x1 = 1*q = w - x24;
    x2 = 1*q = w - x23;
    x3 = 2*q = w - x22;
    x4 = 2*q = w - x21;
    x5 = 3*q = w - x20;
    x6 = 3*q = w - x19;
    x7 = 2*q = w - x18;
    x8 = 2*q = w - x17;
    x9 = 4*q = w - x16;
    x10 = 4*q = w - x15;
    x11 = 5*q = w - x14;
    x12 = 5*q = w - x13;

    y1 = 1*q = y24;
    y2 = 2*q = y23;
    y3 = 2*q = y22;
    y4 = 3*q = y21;
    y5 = 4*q = y20;
    y6 = 8*q = y19;
    y7 = 9*q = y18;
    y8 = 11*q = y17;
    y9 = 11*q = y16;
    y10 = 10*q = y15;
    y11 = 10*q = y14;
    y12 = 11*q = y13;

    path p;
    p = z1 -- z2 -- z3 -- z4 -- z5 -- z6
        -- z7 -- z8 -- z9 -- z10 -- z11 -- z12
        -- z13 -- z14 -- z15 -- z16 -- z17 -- z18
        -- z19 -- z20 -- z21 -- z22 -- z23 -- z24
        -- cycle;

    pickup pencircle xscaled 0.03w yscaled 0.03w;
    penlabels(range 1 thru 24);
enddef;
beginchar(0, 1em#, 1em#, 0); "Rook - mask";
    rook_outline;
    fill p;
endchar;
beginchar(1, 1em#, 1em#, 0); "Rook - white";
    rook_outline;
    draw p;
    draw z3 -- z22;
    draw z4 -- z21;
    draw z5 -- z20;
    draw z6 -- z19;
    draw z7 -- z18;
endchar;
beginchar(2, 1em#, 1em#, 0); "Rook - black";
    rook_outline;
    fill p;
    cullit;
    undraw z3 -- z22;
    undraw z4 -- z21;
    undraw z5 -- z20;
    undraw z6 -- z19;
    undraw z7 -- z18;
    cullit;
    draw p;
endchar;

%%%%%%%%%%%%%%%%%%%% CROCODILE %%%%%%%%%%%%%%%%%%%%
def crocodile_outline =
    numeric q;
    q = round 1/12em;

    x1 = 7*q;
    x2 = 8*q;
    x3 = 2*q;
    x4 = 2*q;
    x5 = 2.5*q;
    x6 = 6*q;
    x7 = 2*q;
    x8 = 3*q;
    x9 = 3*q;
    x10 = 6*q;
    x11 = 9*q;
    x12 = 11*q;
    x13 = 10.5*q;

    x14 = 8*q;
    x15 = 9*q;

    y1 = 1*q;
    y2 = 3*q;
    y3 = 3*q;
    y4 = 5*q;
    y5 = 4*q;
    y6 = 4*q;
    y7 = 7*q;
    y8 = 8.5*q;
    y9 = 7.5*q;
    y10 = 5.5*q;
    y11 = 6*q;
    y12 = 4*q;
    y13 = 1*q;

    y14 = 5*q;
    y15 = 4.5*q;

    path p;
    p = z1 -- z2 -- z3 -- z4 -- z5
        -- z6 -- z7 -- z8 -- z9
        -- z10 -- z11 .. z12 .. z13
        --cycle;

    pickup pencircle xscaled 0.03w yscaled 0.03w;
    penlabels(range 1 thru 15);
enddef;
beginchar(3, 1em#, 1em#, 0); "Crocodile - mask";
    crocodile_outline;
    fill p;
endchar;
beginchar(4, 1em#, 1em#, 0); "Crocodile - white";
    crocodile_outline;
    draw p;
    draw z14 ..  z15;
endchar;
beginchar(5, 1em#, 1em#, 0); "Crocodile - black";
    crocodile_outline;
    fill p;
    cullit;
    undraw z14 ..  z15;
    cullit;
    draw p;
endchar;

%%%%%%%%%%%%%%%%%%%% PAWN1 %%%%%%%%%%%%%%%%%%%%
def pawn_one_outline =
    numeric q;
    q = round 1/12em;

    x1 = 2*q = w - x11;
    x2 = 5*q = w - x10;
    x3 = 3*q = w - x9;
    x4 = 5.5*q = w - x8;
    x5 = 5*q = w - x7;
    x6 = 1/2*w;

    y1 = 1*q = y11;
    y2 = 4*q = y10;
    y3 = 6*q = y9;
    y4 = 9*q = y8;
    y5 = 10*q = y7;
    y6 = 11*q;

    path p;
    p = z1{up} .. z2{right}--z2{left} .. z3 .. z4{right}--z4{left} .. z5{up}
        .. z6{right}
        .. z7{down} .. z8{left}--z8{right} .. z9 .. z10{left}--z10{right} .. z11{down}
        -- cycle;

    pickup pencircle xscaled 0.03w yscaled 0.03w;
    penlabels(range 1 thru 11);
enddef;
beginchar(6, 1em#, 1em#, 0); "Pawn1 - mask";
    pawn_one_outline;
    fill p;
endchar;
beginchar(7, 1em#, 1em#, 0); "Pawn1 - white";
    pawn_one_outline;
    draw p;
endchar;
beginchar(8, 1em#, 1em#, 0); "Pawn1 - black";
    pawn_one_outline;
    fill p;
    cullit;
    draw p;
endchar;

%%%%%%%%%%%%%%%%%%%% PAWN2 %%%%%%%%%%%%%%%%%%%%
def pawn_two_outline =
    numeric q;
    q = round 1/12em;

    x1 = 2*q = w - x11;
    x2 = 5*q = w - x10;
    x3 = 3*q = w - x9;
    x4 = 5.5*q = w - x8;
    x5 = 5*q = w - x7;
    x6 = 1/2*w;

    y1 = 1*q = y11;
    y2 = 4*q = y10;
    y3 = 6*q = y9;
    y4 = 9*q = y8;
    y5 = 10*q = y7;
    y6 = 11*q;

    path p;
    p = z1 -- z2 -- z3 -- z4 -- z5
        -- z6
        -- z7 -- z8 -- z9 -- z10 -- z11
        -- cycle;

    pickup pencircle xscaled 0.03w yscaled 0.03w;
    penlabels(range 1 thru 11);
enddef;
beginchar(9, 1em#, 1em#, 0); "Pawn2 - mask";
    pawn_two_outline;
    fill p;
endchar;
beginchar(10, 1em#, 1em#, 0); "Pawn2 - white";
    pawn_two_outline;
    draw p;
endchar;
beginchar(11, 1em#, 1em#, 0); "Pawn2 - black";
    pawn_two_outline;
    fill p;
    cullit;
    draw p;
endchar;

%%%%%%%%%%%%%%%%%%%% PAWN3 %%%%%%%%%%%%%%%%%%%%
def pawn_three_outline =
    numeric q;
    q = round 1/12em;

    x1 = 2.5*q = w - x11;
    x2 = 5*q = w - x10;
    x3 = 4*q = w - x9;
    x4 = 5.5*q = w - x8;
    x5 = 5*q = w - x7;
    x6 = 1/2*w;

    y1 = 1*q = y11;
    y2 = 5*q = y10;
    y3 = 7*q = y9;
    y4 = 9*q = y8;
    y5 = 10*q = y7;
    y6 = 11*q;

    path p;
    p = z1{up} .. z2{right}--z2{left} .. z3 .. z4{right}--z4{left} .. z5{up}
        .. z6{right}
        .. z7{down} .. z8{left}--z8{right} .. z9 .. z10{left}--z10{right} .. z11{down}
        -- cycle;

    pickup pencircle xscaled 0.03w yscaled 0.03w;
    penlabels(range 1 thru 11);
enddef;
beginchar(12, 1em#, 1em#, 0); "Pawn3 - mask";
    pawn_three_outline;
    fill p;
endchar;
beginchar(13, 1em#, 1em#, 0); "Pawn3 - white";
    pawn_three_outline;
    draw p;
endchar;
beginchar(14, 1em#, 1em#, 0); "Pawn3 - black";
    pawn_three_outline;
    fill p;
    cullit;
    draw p;
endchar;

%%%%%%%%%%%%%%%%%%%% PAWN4 %%%%%%%%%%%%%%%%%%%%
def pawn_four_outline =
    numeric q;
    q = round 1/12em;

    x1 = 2.5*q = w - x11;
    x2 = 5*q = w - x10;
    x3 = 2*q = w - x9;
    x4 = 5.5*q = w - x8;
    x5 = 5*q = w - x7;
    x6 = 1/2*w;

    y1 = 1*q = y11;
    y2 = 5*q = y10;
    y3 = 7*q = y9;
    y4 = 9*q = y8;
    y5 = 10*q = y7;
    y6 = 11*q;

    path p;
    p = z1{up} .. z2{right}--z2{left} .. z3 .. z4{right}--z4{left} .. z5{up}
        .. z6{right}
        .. z7{down} .. z8{left}--z8{right} .. z9 .. z10{left}--z10{right} .. z11{down}
        -- cycle;

    pickup pencircle xscaled 0.03w yscaled 0.03w;
    penlabels(range 1 thru 11);
enddef;
beginchar(15, 1em#, 1em#, 0); "Pawn4 - mask";
    pawn_four_outline;
    fill p;
endchar;
beginchar(16, 1em#, 1em#, 0); "Pawn4 - white";
    pawn_four_outline;
    draw p;
endchar;
beginchar(17, 1em#, 1em#, 0); "Pawn4 - black";
    pawn_four_outline;
    fill p;
    cullit;
    draw p;
endchar;

%%%%%%%%%%%%%%%%%%%% MONKEY %%%%%%%%%%%%%%%%%%%%
def monkey_outline =
    numeric q;
    q = round 1/12em;

    x1 = 6*q;
    x2 = 4*q = w - x16;
    x3 = 5*q = w - x15;
    x4 = 2*q = w - x14;
    x5 = 2*q = w - x13;
    x6 = 1*q = w - x12;
    x7 = 1*q = w - x11;
    x8 = 2*q = w - x10;
    x9 = 6*q;
    x17 = 6*q;
    x18 = 6*q;
    x19 = 6*q;
    x20 = 5*q = w - x21;
    x22 = 5*q = w - x23;
    x24 = 4*q = w - x25;

    y1 = 1*q;
    y2 = 3*q = y16;
    y3 = 5*q = y15;
    y4 = 5*q = y14;
    y5 = 7*q = y13;
    y6 = 8.5*q = y12;
    y7 = 10*q = y11;
    y8 = 9*q = y10;
    y9 = 11*q;
    y17 = 2.5*q;
    y18 = 6*q;
    y19 = 9*q;
    y20 = 3*q = y21;
    y22 = 7*q = y23;
    y24 = 8*q = y25;

    path p;
    p = z1{-1,1} .. z2{up}
        & z2{-1,1} .. z4 .. z5 .. z6 .. z7{right} .. z8{down}
        & z8{up} .. z9{right} .. z10{down}
        & z10{up} .. z11{right} .. z12 .. z13 .. z14 .. z16{-1,-1}
        & z16{down} .. z1{-1,-1}
        & cycle;

    pickup pencircle xscaled 0.03w yscaled 0.03w;
    penlabels(range 1 thru 25);
enddef;
beginchar(18, 1em#, 1em#, 0); "Monkey - mask";
    monkey_outline;
    fill p;
endchar;
beginchar(19, 1em#, 1em#, 0); "Monkey - white";
    monkey_outline;
    draw p;
    draw z2{up} .. z3{1,1};
    draw z16{up} .. z15{-1,1};
    draw z17 -- z18;
    draw z20 .. z17{right} .. z21;
    draw z22 .. z18{right} .. z23;
    pickup pencircle xscaled 0.1w yscaled 0.1w;
    drawdot z24;
    drawdot z25;
endchar;
beginchar(20, 1em#, 1em#, 0); "Monkey - black";
    monkey_outline;
    fill p;
    cullit;
    undraw z2{up} .. z3{1,1};
    undraw z16{up} .. z15{-1,1};
    undraw z17 -- z18;
    undraw z20 .. z17{right} .. z21;
    undraw z22 .. z18{right} .. z23;
    pickup pencircle xscaled 0.1w yscaled 0.1w;
    undrawdot z24;
    undrawdot z25;
    cullit;
    pickup pencircle xscaled 0.03w yscaled 0.03w;
    draw p;
endchar;

%%%%%%%%%%%%%%%%%%%% HORNED %%%%%%%%%%%%%%%%%%%%
def horned_outline =
    numeric q;
    q = round 1/12em;

    x1 = 2*q = w - x9;
    x2 = 5*q = w - x8;
    x3 = 5*q = w - x7;
    x4 = 3*q = w - x6;
    x5 = 1/2*w;

    y1 = 1*q = y9;
    y2 = 3*q = y8;
    y3 = 8*q = y7;
    y4 = 11*q = y6;
    y5 = 10*q;

    path p;
    p = z1{1,1} .. z2{right} -- z2{up} .. z3 .. z4
        -- z5 -- z6 .. z7 .. z8{down} -- z8{right} .. z9{1,-1}
        -- cycle;
    pickup pencircle xscaled 0.03w yscaled 0.03w;
    penlabels(range 1 thru 9);
enddef;
beginchar(21, 1em#, 1em#, 0); "Horned - mask";
    horned_outline;
    fill p;
endchar;
beginchar(22, 1em#, 1em#, 0); "Horned - white";
    horned_outline;
    draw p;
    draw z2 -- z8;
    fill 1/2[z3,z5] -- 1/2[z5,z7] -- 1/2[z7,z3] -- cycle;
endchar;
beginchar(23, 1em#, 1em#, 0); "Horned - black";
    horned_outline;
    fill p;
    cullit;
    undraw z2 -- z8;
    unfill 1/2[z3,z5] -- 1/2[z5,z7] -- 1/2[z7,z3] -- cycle;
    cullit;
    draw p;
endchar;

%%%%%%%%%%%%%%%%%%%% FIRZAN %%%%%%%%%%%%%%%%%%%%
def firzan_outline =
    numeric q;
    q = round 1/12em;

    x1 = 1*q = w - x13;
    x2 = 1*q = w - x12;
    x3 = 3*q = w - x11;
    x4 = 4*q = w - x10;
    x5 = 2*q = w - x9;
    x6 = 5*q = w - x8;
    x7 = 6*q;

    x14 = 6*q;
    x15 = 4.5*q = w - x18;
    x16 = 4.5*q = w - x17;

    x19 = 6*q;

    y1 = 1*q = y13;
    y2 = 2*q = y12;
    y3 = 3*q = y11;
    y4 = 6*q = y10;
    y5 = 9*q = y9;
    y6 = 10*q = y8;
    y7 = 11*q;

    y14 = 6*q;
    y15 = 4.5*q = y18;
    y16 = 7.5*q = y17;

    y19 = 9*q;

    path p;
    p = z1 -- z2 -- z3 .. z4{up} .. z5 .. z6{right}
        -- z7
        -- z8{right} .. z9 .. z10{down} .. z11
        -- z12 -- z13 -- cycle;
    path px;
    px = z15 -- (1/2[z14, 1/2[z15,z16]])
        -- z16 -- (1/2[z14, 1/2[z16,z17]])
        -- z17 -- (1/2[z14, 1/2[z17,z18]])
        -- z18 -- (1/2[z14, 1/2[z18,z15]])
        -- cycle;
    path pd;
    pd = z6 -- z7 -- z8 -- z19 -- cycle;

    pickup pencircle xscaled 0.03w yscaled 0.03w;
    penlabels(range 1 thru 18);
enddef;
beginchar(24, 1em#, 1em#, 0); "Firzan - mask";
    firzan_outline;
    fill p;
endchar;
beginchar(25, 1em#, 1em#, 0); "Firzan - white";
    firzan_outline;
    draw p;
    draw z2 -- z12;
    draw z3 -- z11;
    fill px;
    fill pd;
    cullit;
    undrawdot z14;
    cullit;
endchar;
beginchar(26, 1em#, 1em#, 0); "Firzan - black";
    firzan_outline;
    fill p;
    cullit;
    undraw z2 -- z12;
    undraw z3 -- z11;
    cullit;
    unfill px;
    cullit;
    unfill pd;
    cullit;
    drawdot z14;
    draw p;
endchar;

%%%%%%%%%%%%%%%%%%%% QUEEN %%%%%%%%%%%%%%%%%%%%
def queen_outline =
    numeric q;
    q = round 1/12em;

    x23 = 1*q = w - x25;
    x24 = 6*q;
    y23 = 8.5*q = y25;
    y24 = 10.5*q;

    path crown_arc;
    crown_arc = z23 .. z24{right} .. z25;

    x1 = 2*q = w - x17;
    x2 = 2.5*q = w - x16;
    x3 = 2.5*q = w - x15;
    x4 = 2*q = w - x14;
    x18 = 6*q;
    x19 = 6*q;
    x20 = 6*q;
    x21 = 6*q;
    x22 = 6*q;

    y1 = 2*q = y17;
    y2 = 3*q = y16;
    y3 = 4*q = y15;
    y4 = 5*q = y14;
    y18 = 1*q;
    y19 = 3*q;
    y20 = 4*q;
    y21 = 5*q;
    y22 = 6*q;

    z5 = point 0*2/4 of crown_arc;
    z7 = point 1*2/4 of crown_arc;
    z9 = point 2*2/4 of crown_arc;
    z11 = point 3*2/4 of crown_arc;
    z13 = point 4*2/4 of crown_arc;

    z5.l = z5 + (-0.5*q, +0.5*q);
    z5.u = z5 + ( 0.0*q, +1.0*q);
    z5.r = z5 + (+0.5*q, +0.5*q);
    z7.l = z7 + (-0.5*q, +0.5*q);
    z7.u = z7 + ( 0.0*q, +1.0*q);
    z7.r = z7 + (+0.5*q, +0.5*q);
    z9.l = z9 + (-0.5*q, +0.5*q);
    z9.u = z9 + ( 0.0*q, +1.0*q);
    z9.r = z9 + (+0.5*q, +0.5*q);
    z11.l = z11 + (-0.5*q, +0.5*q);
    z11.u = z11 + ( 0.0*q, +1.0*q);
    z11.r = z11 + (+0.5*q, +0.5*q);
    z13.l = z13 + (-0.5*q, +0.5*q);
    z13.u = z13 + ( 0.0*q, +1.0*q);
    z13.r = z13 + (+0.5*q, +0.5*q);

    path pla, plb, plc, pld, ple;
    pla = z1{1,-1} .. z18{right} .. z17{1,1};
    plb = z1{1,1} .. z19{right} .. z17{1,-1};
    plc = z2{1,1} .. z20{right} .. z16{1,-1};
    pld = z3{1,1} .. z21{right} .. z15{1,-1};
    ple = z4{1,1} .. z22{right} .. z14{1,-1};

    z6 = point 1*2/8 of ple;
    z8 = point 3*2/8 of ple;
    z10 = point 5*2/8 of ple;
    z12 = point 7*2/8 of ple;

    path p;
    p = z1 .. z2 .. z3 .. z4
        -- z5 & (z5{left} .. z5.l{up} .. z5.u{right} .. z5{left}) & z5
        -- z6
        -- z7 & (z7{left} .. z7.l{up} .. z7.u{right} .. z7{left}) & z7
        -- z8
        -- z9 & (z9{left} .. z9.l{up} .. z9.u{right} .. z9{left}) & z9
        -- z10
        -- z11 & (z11{left} .. z11.l{up} .. z11.u{right} .. z11{left}) & z11
        -- z12
        -- z13 & (z13{left} .. z13.l{up} .. z13.u{right} .. z13{left}) & z13
        -- z14
        .. z15 .. z16 .. z17 & (reverse pla) & cycle;

    pickup pencircle xscaled 0.03w yscaled 0.03w;
    penlabels(range 1 thru 25);
enddef;
beginchar(27, 1em#, 1em#, 0); "Queen - mask";
    queen_outline;
    fill p;
endchar;
beginchar(28, 1em#, 1em#, 0); "Queen - white";
    queen_outline;
    draw p;
    draw plb;
    draw plc;
    draw pld;
    draw ple;
endchar;
beginchar(29, 1em#, 1em#, 0); "Queen - black";
    queen_outline;
    fill p;
    cullit;
    undraw plb;
    undraw plc;
    undraw pld;
    %undraw ple;
    cullit;
    draw p;
endchar;

%%%%%%%%%%%%%%%%%%%% KING %%%%%%%%%%%%%%%%%%%%
def king_outline =
    numeric q;
    q = round 1/12em;

    x1 = 3*q = w - x16;
    x2 = 3*q = w - x15;
    x3 = 3*q = w - x14;
    x4 = 1*q = w - x13;
    x5 = 2.5*q = w - x12;
    x6 = 4.5*q = w - x11;
    x7 = x8 = x9 = x10 = 6*q;
    x17 = x19 = 6*q;
    x18 = 5*q = w - x20;

    y1 = 1.5*q = y16;
    y2 = 2.5*q = y15;
    y3 = 3.5*q = y14;
    y4 = 8*q = y13;
    y5 = 9*q = y12;
    y6 = 8*q = y11;
    y7 = 4*q;
    y8 = 3*q;
    y9 = 2*q;
    y10 = 1*q;
    y18 = y20 = 10*q;
    y17 = 9*q;
    y19 = 11*q;

    path p;
    p = z1 -- z2 -- z3
        & z3 .. z4{up} .. z5{right} .. z6{1,-1} .. z7{down}
        & z7{up} .. z11{1,1} .. z12{right} .. z13{down} .. z14
        & z14 -- z15 -- z16
        & z16 .. z10{left} .. z1
        & cycle;
    path ph;
    ph = z6 .. z17{right} .. z11 -- z7 -- cycle;
    pickup pencircle xscaled 0.03w yscaled 0.03w;
    penlabels(range 1 thru 20);
enddef;
beginchar(30, 1em#, 1em#, 0); "King - mask";
    king_outline;
    fill p;
    fill ph;
endchar;
beginchar(31, 1em#, 1em#, 0); "King - white";
    king_outline;
    draw p;
    draw z6 .. z17{right} .. z11;
    draw z1 .. z9{right} .. z16;
    draw z2 .. z8{right} .. z15;
    draw z3 .. z7{right} .. z14;
    draw z18 -- z20; draw z17 -- z19;
endchar;
beginchar(32, 1em#, 1em#, 0); "King - black";
    king_outline;
    fill p;
    fill ph;
    cullit;
    undraw z1 .. z9{right} .. z16;
    undraw z2 .. z8{right} .. z15;
    undraw z3 .. z7{right} .. z14;
    cullit;
    pickup pencircle xscaled 0.08w yscaled 0.08w;
    undraw z3 .. z4{up} .. z5{right} .. z6 .. z7{down}
        & z7{up} .. z11 .. z12{right} .. z13{down} .. z14;
    cullit;
    undraw z6 .. z17{right} .. z11;
    pickup pencircle xscaled 0.03w yscaled 0.03w;
    cullit;
    draw p;
    draw z6 .. z17{right} .. z11;
    draw z18 -- z20; draw z17 -- z19;
endchar;

%%%%%%%%%%%%%%%%%%%% BISHOP %%%%%%%%%%%%%%%%%%%%
def bishop_outline =
    numeric q;
    q = round 1/12em;

    x1 = 1*q = w - x12;
    x2 = 1*q = w - x11;
    x3 = 6*q;
    x4 = 3*q = w - x10;
    x5 = 4*q = w - x9;
    x6 = 3*q = w - x8;
    x7 = 6*q;
    x13 = 6*q;
    x14 = 6*q;
    x15 = 6*q;
    x16 = 6*q;
    x17 = 5*q;
    x18 = 6*q;
    x19 = 7*q;

    y1 = 1*q = y12;
    y2 = 2*q = y11;
    y3 = 2.5*q;
    y4 = 3.5*q = y10;
    y5 = 5*q = y9;
    y6 = 7*q = y8;
    y7 = 10*q;
    y13 = 1.5*q;
    y14 = 4.25*q;
    y15 = 5.5*q;
    y16 = 6.5*q;
    y17 = 7.5*q;
    y18 = 8.5*q;
    y19 = 7.5*q;

    z7.u = z7 + ( 0.0*q,+1.0*q);
    z7.l = z7 + (-0.5*q,+0.5*q);
    z7.r = z7 + (+0.5*q,+0.5*q);

    path p;
    p = z1 -- z2 & z2{1,1} .. z3{1,1}
        & z3{left} .. z4{-1,1}
        & z4 -- z5
        & z5 .. z6{up} .. z7{1,1}
        & z7{1,-1} .. z8{down} .. z9
        & z9 -- z10
        & z10{-1,-1} .. z3{left}
        & z3{1,-1} .. z11{1,-1}
        & z11 -- z12
        & z12{-1,1} .. z13{-1,1}
        & z13{-1,-1} .. z1{-1,-1}
        & cycle;

    path po;
    po = z7{left} .. z7.l{up} .. z7.u{right} .. z7.r{down} .. cycle;

    pickup pencircle xscaled 0.03w yscaled 0.03w;
    penlabels(range 1 thru 19);
enddef;
beginchar(33, 1em#, 1em#, 0); "Bishop - mask";
    bishop_outline;
    fill p;
    fill po;
endchar;
beginchar(34, 1em#, 1em#, 0); "Bishop - white";
    bishop_outline;
    draw p;
    draw po;
    draw z4 .. z14{right} .. z10;
    draw z5 .. z15{right} .. z9;
    draw z16 -- z18;
    draw z17 -- z19;
endchar;
beginchar(35, 1em#, 1em#, 0); "Bishop - black";
    bishop_outline;
    fill p;
    cullit;
    undraw z4 .. z14{right} .. z10;
    undraw z5 .. z15{right} .. z9;
    undraw z16 -- z18;
    undraw z17 -- z19;
    cullit;
    unfill po;
    cullit;
    draw p;
    draw po;
endchar;

%%%%%%%%%%%%%%%%%%%% KNIGHT %%%%%%%%%%%%%%%%%%%%
def knight_outline =
    numeric q;
    q = round 1/12em;

    x1 = 4*q;
    x2 = 6.5*q;
    x3 = 6.5*q;
    x4 = 3*q;
    x5 = 2.75*q;
    x6 = 2*q;
    x7 = 1.25*q;
    x8 = 2.5*q;
    x9 = 3*q;
    x10 = 3.9*q;
    x11 = 3.5*q;
    x12 = 4.25*q;
    x13 = 4.5*q;
    x14 = 5*q;
    x15 = 5*q;
    x16 = 5.5*q;
    x17 = 6.25*q;
    x18 = 6.25*q;
    x19 = 9*q;
    x20 = 10*q;
    x21 = 11*q;
    x22 = 10.5*q;
    x23 = 10.25*q;
    x24 = 9*q;
    x25 = 6.5*q;
    x26 = 2*q;
    x27 = 3.5*q;
    x28 = 4.25*q;
    x29 = 4*q;
    x30 = 4*q;
    x31 = 3*q;

    y1 = 1*q;
    y2 = 6*q;
    y3 = 7.75*q;
    y4 = 3.8*q;
    y5 = 4*q;
    y6 = 4*q;
    y7 = 5*q;
    y8 = 7*q;
    y9 = 8.75*q;
    y10 = 9*q;
    y11 = 11*q;
    y12 = 10.25*q;
    y13 = 10*q;
    y14 = 10*q;
    y15 = 9.5*q;
    y16 = 11*q;
    y17 = 9.25*q;
    y18 = 10*q;
    y19 = 8.5*q;
    y20 = 7*q;
    y21 = 1*q;
    y22 = 1*q;
    y23 = 5*q;
    y24 = 8*q;
    y25 = 9.75*q;
    y26 = 5*q;
    y27 = 7.5*q;
    y28 = 8.5*q;
    y29 = 8*q;
    y30 = 9.5*q;
    y31 = 4.5*q;

    path p;
    p = z1{up} .. z2{up}
        & z2{-1,-1} .. z4{-1,-1} & z4 .. z5{up}
        & z5 .. z7 .. z8 .. z9 .. z10{right}
        & z10{left} .. z11{up}
        & z11 .. z12 .. z13{1,-1}
        & z13{right} .. z14
        & z14{up} .. z16{up}
        & z16 .. z18
        & z18 .. z19 .. z20 .. z21{down}
        -- cycle;

    pickup pencircle xscaled 0.03w yscaled 0.03w;
    penlabels(range 1 thru 31);
enddef;
beginchar(36, 1em#, 1em#, 0); "Knight - mask";
    knight_outline;
    fill p;
endchar;
beginchar(37, 1em#, 1em#, 0); "Knight - white";
    knight_outline;
    draw p;
    draw z15 .. z14;
    draw z17 .. z18{z16-z18};
    draw z3 .. z2{-1,-1};
    draw z5 .. z31{up};
    draw z22 .. z23 .. z24 .. z25;
    draw z30 .. z13{right};
    pickup pencircle xscaled 0.04w yscaled 0.08w rotated -30;
    drawdot z29;
    pickup pencircle xscaled 0.05w yscaled 0.06w rotated -20;
    drawdot z26;
endchar;
beginchar(38, 1em#, 1em#, 0); "Knight - black";
    knight_outline;
    fill p;
    cullit;
    undraw z3 .. z2{-1,-1};
    undraw z5 .. z31{up};
    undraw z22 .. z23 .. z24 .. z25;
    cullit;
    pickup pencircle xscaled 0.04w yscaled 0.08w rotated -30;
    undrawdot z29;
    pickup pencircle xscaled 0.05w yscaled 0.06w rotated -20;
    undrawdot z26;
    pickup pencircle xscaled 0.03w yscaled 0.03w;
    cullit;
    draw p;
endchar;

%%%%%%%%%%%%%%%%%%%% ELEPHANT %%%%%%%%%%%%%%%%%%%%
def elephant_outline =
    numeric q;
    q = round 1/12em;

    x1 = 5.5*q = w - x13;
    x2 = 5*q = w - x12;
    x3 = 4.5*q = w - x11;
    x4 = 3.5*q = w - x10;
    x5 = 4*q = w - x9;
    x6 = 4.5*q = w - x8;
    x7 = 6*q;
    x14 = 4.5*q = w - x15;
    x16 = 4*q = w - x19;
    x17 = 1*q = w - x20;
    x18 = 2.5*q = w - x21;
    x22 = 3*q = w - x24;
    x23 = 4*q = w - x25;

    y1 = 1*q = y13;
    y2 = 4.5*q = y12;
    y3 = 5.5*q = y11;
    y4 = 8*q = y10;
    y5 = 10*q = y9;
    y6 = 10.5*q = y8;
    y7 = 11*q;
    y14 = 8.5*q = y15;
    y16 = 5*q = y19;
    y17 = 8*q = y20;
    y18 = 11*q = y21;
    y22 = 2.5*q = y24;
    y23 = 1*q = y25;

    path p;
    p = z1{up} .. z2 .. z3 .. z4 .. z5 .. z6 .. z7{right}
        .. z8 .. z9 .. z10 .. z11 .. z12 .. z13{down}
        -- cycle;
    path pel, per;
    pel = z16{-1,1} .. z17{up} .. z18{right} .. z6;
    per = z19{1,1} .. z20{up} .. z21{left} .. z8;
    path ptl, ptr;
    ptl = z3 .. z16 .. z22{down} .. z23{1,-1} & z23{-1,1} .. z2;
    ptr = z11 .. z19 .. z24{down} .. z25{-1,-1} & z25{1,1} .. z12;

    pickup pencircle xscaled 0.03w yscaled 0.03w;
    penlabels(range 1 thru 25);
enddef;
beginchar(39, 1em#, 1em#, 0); "Elephant - mask";
    elephant_outline;
    fill p;
    fill ptl & z2 -- cycle;
    fill reverse (ptr & z12 -- cycle);
    fill pel & z6 -- z5 -- z4 -- z3 -- cycle;
    fill per & z8 -- z9 -- z10 -- z11 -- cycle;
endchar;
beginchar(40, 1em#, 1em#, 0); "Elephant - white";
    elephant_outline;
    draw p;
    draw pel;
    draw per;
    draw ptl;
    draw ptr;
    pickup pencircle xscaled 0.08w yscaled 0.08w;
    drawdot z14;
    drawdot z15;
    pickup pencircle xscaled 0.03w yscaled 0.03w;
endchar;
beginchar(41, 1em#, 1em#, 0); "Elephant - black";
    elephant_outline;
    fill p;
    fill ptl & z2 -- cycle;
    fill reverse (ptr & z12 -- cycle);
    fill pel & z6 -- z5 -- z4 -- z3 -- cycle;
    fill per & z8 -- z9 -- z10 -- z11 -- cycle;
    cullit;
    pickup pencircle xscaled 0.08w yscaled 0.08w;
    undraw subpath (2,5) of p;
    undraw subpath (7,10) of p;
    pickup pencircle xscaled 0.03w yscaled 0.03w;
    cullit;
    fill p;
    fill ptl & z2 -- cycle;
    fill reverse (ptr & z12 -- cycle);
    cullit;
    pickup pencircle xscaled 0.08w yscaled 0.08w;
    undrawdot z14;
    undrawdot z15;
    pickup pencircle xscaled 0.03w yscaled 0.03w;
    cullit;
    draw p;
    draw pel;
    draw per;
    draw ptl;
    draw ptr;
endchar;

%%%%%%%%%%%%%%%%%%%% GIRAFFE %%%%%%%%%%%%%%%%%%%%
def giraffe_outline =
    numeric q;
    q = round 1/12em;

    x1 = 1*q;
    x2 = 3*q;
    x3 = 5.75*q;
    x4 = 7*q;
    x5 = 7.25*q;
    x6 = 8*q;
    x7 = 9*q;
    x8 = 10*q;
    x9 = 11*q;
    x10 = 10.5*q;
    x11 = 9.5*q;
    x12 = 8.5*q;
    x13 = 7*q;
    x14 = 6.5*q;
    x15 = 5.5*q;
    x16 = 4*q;
    x17 = 8*q;

    x18 = 6.5*q;
    x19 = 5*q;
    x20 = 6.75*q;
    x21 = 6*q;
    x22 = 7.75*q;

    y1 = 1*q;
    y2 = 5*q;
    y3 = 7.75*q;
    y4 = 8.5*q;
    y5 = 8.5*q;
    y6 = 8.5*q;
    y7 = 7.5*q;
    y8 = 6.5*q;
    y9 = 5.25*q;
    y10 = 5*q;
    y11 = 5.25*q;
    y12 = 5*q;
    y13 = 5.5*q;
    y14 = 6*q;
    y15 = 4*q;
    y16 = 1*q;
    y17 = 7*q;

    y18 = 7.25*q;
    y19 = 8.75*q;
    y20 = 7.75*q;
    y21 = 10*q;
    y22 = 10*q;

    path p;
    p = z1 .. z2 .. z3 .. z4{right} .. z5 .. z6
        .. z7 .. z8 .. z9{down} .. z10{left} .. z11{-1,1}
        & z11{-1,-1} .. z12{left} .. z13{-1,1}
        & z13 .. z15 .. z16
        -- cycle;

    path pe;
    pe = z18 .. z3 .. z19 & z19 .. z20;

    path phl, phr;
    phl = z20{-1,1} .. {curl 1.5}z21{curl 1.5} .. z4{down};
    phr = z5{up} .. {curl 1.5}z22{curl 1.5} .. z6{down};

    path psa, psb, psc;
    psa = (2,1.5)*q -- (3.5,2)*q -- (3,3)*q -- (2.5,2.5)*q -- cycle;
    psb = (4,3.5)*q -- (5,4)*q -- (4,5)*q -- (3,4)*q -- cycle;
    psc = (5,5)*q -- (6,5.5)*q -- (5,6.5)*q -- (4.5,5.5)*q -- cycle;

    pickup pencircle xscaled 0.03w yscaled 0.03w;
    penlabels(range 1 thru 22);
enddef;
beginchar(42, 1em#, 1em#, 0); "Giraffe - mask";
    giraffe_outline;
    fill p;
    fill pe -- cycle;
    fill phl -- cycle;
    fill phr -- cycle;
endchar;
beginchar(43, 1em#, 1em#, 0); "Giraffe - white";
    giraffe_outline;
    draw p;
    cullit;
    unfill pe -- cycle;
    unfill phl -- cycle;
    cullit;
    fill psa;
    fill psb;
    fill psc;
    draw pe;
    draw phl;
    draw phr;
    draw z13{-1,1} .. z14;
    pickup pencircle xscaled 0.06w yscaled 0.08w rotated 20;
    drawdot z17;
    pickup pencircle xscaled 0.03w yscaled 0.03w;
endchar;
beginchar(44, 1em#, 1em#, 0); "Giraffe - black";
    giraffe_outline;
    fill p;
    fill pe -- cycle;
    fill phl -- cycle;
    fill phr -- cycle;
    cullit;
    pickup pencircle xscaled 0.06w yscaled 0.08w rotated 20;
    undrawdot z17;
    pickup pencircle xscaled 0.03w yscaled 0.03w;
    cullit;
    unfill psa;
    unfill psb;
    unfill psc;
    cullit;
    draw p;
    draw pe;
    draw phl;
    draw phr;
endchar;

%%%%%%%%%%%%%%%%%%%% LION %%%%%%%%%%%%%%%%%%%%
def lion_outline =
    numeric q;
    q = round 1/12em;

    x1 = 6*q;
    x2 = 2*q = w - x4;
    x3 = 6*q;

    x5 = 3*q = w - x8;
    x6 = 3*q = w - x9;
    x7 = 4*q = w - x10;

    x11 = 6*q;
    x12 = 5*q = w - x18;
    x13 = 3.5*q = w - x17;
    x14 = 4.5*q = w - x16;
    x15 = 6*q;

    x19 = 6*q;
    x20 = 6*q;
    x21 = 4.75*q = w - x22;
    x23 = 5.25*q = w - x24;
    
    y1 = 1*q;
    y2 = 9*q = y4;
    y3 = 11*q;

    y5 = 7.5*q = y8;
    y6 = 9*q = y9;
    y7 = 9*q = y10;

    y11 = 3.5*q;
    y12 = 4.25*q = y18;
    y13 = 6.5*q = y17;
    y14 = 8.5*q = y16;
    y15 = 7*q;

    y19 = 4.75*q;
    y20 = 5.5*q;
    y21 = 7*q = y22;
    y23 = 5.5*q = y24;
    
    path p;
    p = z1{-1,1} .. z2{up}
        & z2{1,1} .. z3{right} .. z4{1,-1}
        & z4{down} .. z1{-1,-1}
        & cycle;

    % looks good large, but scaled down is unclear
    path pf;
    pf = z13 -- z14 & z14{right} .. z15{down} & z15{up} .. z16{right} & z16 -- z17
        & z17{left} .. z18{down} -- z11 -- z12{up} .. z13{left};
    
    path pel, per;
    pel = z5 --- z6 --- z7 --- cycle;
    per = z8 --- z9 --- z10 --- cycle;

    pickup pencircle xscaled 0.03w yscaled 0.03w;
    penlabels(range 1 thru 24);
enddef;
beginchar(45, 1em#, 1em#, 0); "Lion - mask";
    lion_outline;
    fill p;
endchar;
beginchar(46, 1em#, 1em#, 0); "Lion - white";
    lion_outline;
    draw p;
    fill pel;
    fill per;
    draw z12 -- z19;
    draw z18 -- z19;
    draw z20 -- z19;
    draw z23 -- z24;
    %draw pf;
    pickup pencircle xscaled 0.07w yscaled 0.1w rotated 20;
    drawdot z21;
    pickup pencircle xscaled 0.07w yscaled 0.1w rotated -20;
    drawdot z22;
    pickup pencircle xscaled 0.03w yscaled 0.03w;
endchar;
beginchar(47, 1em#, 1em#, 0); "Lion - black";
    lion_outline;
    fill p;
    cullit;
    pickup pencircle xscaled 0.07w yscaled 0.1w rotated 20;
    undrawdot z21;
    pickup pencircle xscaled 0.07w yscaled 0.1w rotated -20;
    undrawdot z22;
    pickup pencircle xscaled 0.03w yscaled 0.03w;
    cullit;
    unfill pel;
    unfill per;
    undraw z12 -- z19;
    undraw z18 -- z19;
    undraw z20 -- z19;
    undraw z23 -- z24;
    %undraw pf;
    cullit;
    draw p;
endchar;



end
