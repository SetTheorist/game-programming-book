beginthaichar("TAW TAHN", "B0", "ta", 2thick# + skinnywd# + thin#, normht#, lvgap# + lvht#);
	q_def(1, thick, 0);
	cup_def(2);
	rhat_def(3);
	q_def(4, thick, thick);
	penpos5(thick, 0);
	penpos6(thin, 90);
	penpos7(thin, 90);
	penpos8(thin, 90);
	penpos9(thin, 180);
	penpos10(thin, 270);
	penpos11(thin, 300);

	x1lft = x3lft = leftmost;
	x1l = x2lft;
	x2rt = x3r = x3rt = rightmost;
	y3top = normht;
	y1top = y3taw - 2thin;

	x4r = x5r = rightmost;
	x6 = 1/2[x7, x5l];
	x7 = x11 = 0.6[x9, x5l];
	x8 = x10 = 1/3[x9l, x7];
	x9r = leftmost;
	y4top = -lvgap;
	y5 = y7l = y10r = - lvgap - lvht;
	y6r + 1/2thin = y11r = y8r = y10l + 2/3dia;
	y9 = 1/2[y8, y10];

	filldraw q_path(1) -- cup_path_i(2) -- rhat_path(3)
		-- reverse cup_path_o(2) -- cycle;
	filldraw q_path(4) -- z5r -- z5l -- cycle;
	filldraw z5l{dir 135} .. z6l{left} .. {dir225}z7l{dir135}
		.. z8l{left} .. z9l{down}
		.. z10l{right} .. {dir 45}z11l -- z11r{dir 225}
		.. z10r{left} .. z9r{up}
		.. z8r{right} .. {dir -45}z7r{dir 45}
		.. z6r{right} .. z5r -- cycle;
	penlabels(range 1 thru 11);
endthaichar;
