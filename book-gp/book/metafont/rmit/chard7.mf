beginthaichar("EU", "D7", "eu", 0, normht# + uvgap# + uvht#, 0);
	med := 1/2[thin, thick];
	if 3med > 1/2normwd: med := 1/6normwd; fi;
	x1l + med = x1r;
	y1l - thin = y1r;
	z1 = 1/2[z1l, z1r];
	x2t = x2b = 1/2[x1, x3];
	y2t = y2b + med;
	z2 = 1/2[z2t, z2b];
	x3l + med = x3r;
	y3l + thin = y3r;
	z3 = 1/2[z3l, z3r];
	penpos4(med, 0);
	penpos5(med, 0);

	x1r = - rightgap;
	x3r = x1l - normwd;
	y1r = normht + uvgap;
	y2t = y1r + uvht - 3/4dia;
	y3l = y1r + thin;
	x4r = x1r;
	y4r = normht + uvgap + uvht;
	z5 = z4 - (2med, 0);
	z6l = (z1{up} .. {left}z2t) intersectionpoint ((x5l, 0) .. (x5l, y4r));
	z6r = (z1{up} .. {left}z2t) intersectionpoint ((x5r, 0) .. (x5r, y4r));

	filldraw z1r -- z3l{up} .. z2t .. {down}cycle;
	unfill z1l -- z3r{up} .. z2b .. {down}cycle;
	filldraw z1r -- z4r -- z4l -- z1l -- cycle;
	filldraw z6r -- z5r -- z5l -- z6l -- cycle;
	penlabels(1,2,3);
endthaichar;
