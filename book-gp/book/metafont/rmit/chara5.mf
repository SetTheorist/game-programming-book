beginthaichar("KAW KOH!N", "A5", "ko", 2thick# + jutout# + normwd#, normht#, 0);
	penpos1(thick, 0);
	penpos2(thick, 0);
	penpos3(thin, 270);
	penpos4(thin, 180);
	penpos5(thin, 90);
	penpos6(thin, 0);
	penpos7(thin, 270);
	penpos8(thin, 270);
	penpos9(thin, 270);
	penpos10(thick, 180);
	penpos11(thick, 180);

	lft x6l = leftmost;
	x6l = x1l - jutout;
	x1 = x2;
	x4r = x2r + dia;
	x3 = x5 = (x2r + x4l) / 2;
	x10 = x11;
	rt x10l = rightmost;
	x8 = 1/2[x6, x10];
	x7 = 1/2[x6, x8];
	x9 = 1/2[x8, x10];
	y1 = y11 = 0;
	top y7 = top y9 = normht;
	y8 = y7 - 0.5curveht;
	y6 = y10 = y7 - 1.2curveht;
	y5 = y6 - 3/2curveht;
	y2 = y5 - 1/3dia;
	y3r = y5r + dia;
	y4 = (y3 + y5) / 2;

	filldraw z1l{up} -- z2l{up} .. z3l{right} .. z4l{down} .. z5l{left}
		.. z6l{up} .. z7l{right} .. z8l{right} .. z9l{right}
		.. {down}z10l -- z11l -- z11r
		-- z10r{up} .. {left}z9r .. {left}z8r .. {left}z7r
		.. z6r{down} .. z5r{right}
		.. z4r{up} .. z3r{left} .. z2r{down} -- z1r{down} -- cycle;
	penlabels(range 1 thru 11);
endthaichar;
