mode_setup;

em#    := 10pt#;
cap#   := 7pt#;
thin#  := 1/3pt#;
thick# := 5/6pt#;
o#     := 1/5pt#;

q#     := 1/12em#;

define_pixels(em, cap);
define_blacker_pixels(thin, thick);
define_corrected_pixels(o);

curve_sidebar = round 1/18em;


%%%%%%%%%%%%%%%%%%%% SYLPH %%%%%%%%%%%%%%%%%%%%
def sylph_outline = 
    numeric q;
    q = round 1/12em;

    x1 = 3*q = w - x18;
    x2 = 3*q = w - x17;
    x3 = 5*q = w - x16;
    x4 = 1*q = w - x15;
    x5 = 5*q = w - x14;
    x6 = 5*q = w - x13;
    x7 = 2*q = w - x12;
    x8 = 5*q = w - x11;
    x9 = 4*q = w - x10;
 
    y1 = 1*q = y18;
    y2 = 2*q = y17;
    y3 = 4*q = y16;
    y4 = 7*q = y15;
    y5 = 6*q = y14;
    y6 = 7*q = y13;
    y7 = 10*q = y12;
    y8 = 9*q = y11;
    y9 = 11*q = y10;
 
    path p;
    p = reverse (z1 -- z2{right}
        .. {up}z3{left} .. {up}z4{down} .. {right}z5{up}
        .. {up}z6{left} .. {up}z7{down} .. {right}z8{up}
        .. {left}z9 -- z10{left}
        .. {down}z11{right} .. {up}z12{down} .. {left}z13{down}
        .. {down}z14{right} .. {up}z15{down}.. {left}z16{down}
        .. {right}z17 -- z18 -- cycle);

    pickup pencircle xscaled 0.03w yscaled 0.03w;
    penlabels(range 1 thru 18);
enddef;
beginchar(0, 1em#, 1em#, 0); "Sylph - mask";
    sylph_outline;
    fill p;
endchar;
beginchar(1, 1em#, 1em#, 0); "Sylph - white";
    sylph_outline;
    draw p;
    draw z2 -- z17;
endchar;

%%%%%%%%%%%%%%%%%%%% PALADIN %%%%%%%%%%%%%%%%%%%%
def paladin_outline = 
    numeric q;
    q = round 1/12em;

    x1 = x3 = 6*q;
    x2 = 2*q = w - x4;
 
    y1 = 1*q;
    y2 = 9*q = y4;
    y3 = 11*q;
 
    path p;
    p = z1{-1,1} .. {up}z2{1,1} .. z3{right} .. {1,-1}z4{down} .. {-1,-1}cycle;

    x5 = 5.5*q = w - x16;
    x6 = 5.5*q = w - x15;
    x7 = 4*q = w - x14;
    x8 = 4*q = w - x13;
    x9 = 5.5*q = w - x12;
    x10 = 5.5*q = w - x11;

    y5 = 3.5*q = y16;
    y6 = 7.0*q = y15;
    y7 = 7.0*q = y14;
    y8 = 8.0*q = y13;
    y9 = 8.0*q = y12;
    y10 = 9.5*q = y11;

    path px;
    px = z5 -- z6 -- z7 -- z8 -- z9 -- z10
        -- z11 -- z12 -- z13 -- z14 -- z15 -- z16
        -- cycle;

    pickup pencircle xscaled 0.03w yscaled 0.03w;
    penlabels(range 1 thru 16);
enddef;
beginchar(3, 1em#, 1em#, 0); "Paladin - mask";
    paladin_outline;
    fill p;
endchar;
beginchar(4, 1em#, 1em#, 0); "Paladin - white";
    paladin_outline;
    draw p;
    %draw px;
    fill px;
endchar;

%%%%%%%%%%%%%%%%%%%% WARRIOR %%%%%%%%%%%%%%%%%%%%
def warrior_outline = 
    numeric q;
    q = round 1/12em;

    x1 = 2*q = w - x11;
    x2 = 2*q = w - x10;
    x3 = 5*q = w - x9;
    x4 = 5*q = w - x8;
    x5 = 3*q = w - x7;
    x6 = 6*q;
    x12 = 6*q;
 
    y1 = 1*q = y11;
    y2 = 2*q = y10;
    y3 = 2*q = y9;
    y4 = 6*q = y8;
    y5 = 8*q = y7;
    y6 = 11*q;
    y12 = 8*q;
 
    path p;
    p = z1 -- z2 -- z3 -- z4 -- z5 -- z6
        -- z7 -- z8 -- z9 -- z10 -- z11 -- cycle;

    pickup pencircle xscaled 0.03w yscaled 0.03w;
    penlabels(range 1 thru 12);
enddef;
beginchar(6, 1em#, 1em#, 0); "Warrior - mask";
    warrior_outline;
    fill p;
endchar;
beginchar(7, 1em#, 1em#, 0); "Warrior - white";
    warrior_outline;
    draw p;
    draw z3 -- z9;
    cullit;
    pickup pencircle xscaled 0.1w yscaled 0.09w;
    drawdot z12;
    pickup pencircle xscaled 0.03w yscaled 0.03w;
endchar;

%%%%%%%%%%%%%%%%%%%% HERO %%%%%%%%%%%%%%%%%%%%
def hero_outline = 
    numeric q;
    q = round 1/12em;

    x1 = 2*q = w - x28;
    x2 = 2*q = w - x27;
    x3 = 5*q = w - x26;
    x4 = 4*q = w - x25;
    x5 = 5*q = w - x24;
    x6 = 4.5*q = w - x23;
    x7 = 3*q = w - x22;
    x8 = 3.5*q = w - x21;
    x9 = 3*q = w - x20;
    x10 = 3.5*q = w - x19;
    x11 = 3*q = w - x18;
    x12 = 4.5*q = w - x17;
    x13 = 5*q = w - x16;
    x14 = 4*q = w - x15;

    x29 = 5*q = x32;
    x30 = 7*q = x31;
 
    y1 = 1*q = y28;
    y2 = 2*q = y27;
    y3 = 2*q = y26;
    y4 = 4*q = y25;
    y5 = 6*q = y24;
    y6 = 6.5*q = y23;
    y7 = 6*q = y22;
    y8 = 7.5*q = y21;
    y9 = 8*q = y20;
    y10 = 8.5*q = y19;
    y11 = 10*q = y18;
    y12 = 9.5*q = y17;
    y13 = 10*q = y16;
    y14 = 11*q = y15;
 
    y29 = 7*q = y30;
    y31 = 9*q = y32;

    path p;
    p = z1 -- z2 -- z3 -- z4 -- z5 -- z6 -- z7 -- z8 -- z9 -- z10 -- z11 -- z12 -- z13 -- z14
        -- z15 -- z16 -- z17 -- z18 -- z19 -- z20 -- z21 -- z22 -- z23 -- z24 -- z25 -- z26 -- z27 -- z28
        -- cycle;

    pickup pencircle xscaled 0.03w yscaled 0.03w;
    penlabels(range 1 thru 32);
enddef;
beginchar(9, 1em#, 1em#, 0); "Hero - mask";
    hero_outline;
    fill p;
endchar;
beginchar(10, 1em#, 1em#, 0); "Hero - white";
    hero_outline;
    draw p;
    draw z3 -- z26;
    draw z29 -- z31;
    draw z30 -- z32;
endchar;

%%%%%%%%%%%%%%%%%%%% MAGE %%%%%%%%%%%%%%%%%%%%
def mage_outline = 
    numeric q;
    q = round 1/12em;

    x1 = 2*q = w - x16;
    x2 = 2*q = w - x15;
    x3 = 3*q = w - x14;
    x4 = 5*q = w - x13;
    x5 = 5*q = w - x12;

    x6 = 2*q = w - x9;
    x7 = 5*q = w - x10;
    x8 = 3*q = w - x11;
 
    y1 = 1*q = y16;
    y2 = 2*q = y15;
    y3 = 3*q = y14;
    y4 = 3*q = y13;
    y5 = 5.5*q;
    y12 = 6.5*q;

    y6 = 4*q;
    y7 = 7*q;
    y8 = 7.5*q;
    y9 = 11*q;
    y10 = 8*q;
    y11 = 7.5*q;

    path p;
    p = z1 -- z2 -- z3 -- z4 -- z5
        -- z6 -- z7 -- z8 -- z9 -- z10 -- z11
        -- z12 -- z13 -- z14 -- z15 -- z16
        -- cycle;

    pickup pencircle xscaled 0.03w yscaled 0.03w;
    penlabels(range 1 thru 16);
enddef;
beginchar(12, 1em#, 1em#, 0); "Mage - mask";
    mage_outline;
    fill p;
endchar;
beginchar(13, 1em#, 1em#, 0); "Mage - white";
    mage_outline;
    draw p;
    draw z2 -- z15;
    draw z4 -- z13;
endchar;

%%%%%%%%%%%%%%%%%%%% DRAGON %%%%%%%%%%%%%%%%%%%%
def dragon_outline = 
    numeric q;
    q = round 1/12em;

    x1 = 4*q = w - x12;
    x2 = 1*q = w - x11;
    x3 = 4*q = w - x10;
    x4 = 5*q = w - x9;
    x18 = 5*q = w - x13;

    x5 = 4.5*q = w - x14;
    x6 = 6*q;
    x7 = 7*q = w - x16;
    x8 = 5.5*q = w - x17;
    x15 = 6*q;
 
    y1 = 1*q = y12;
    y2 = 6*q = y11;
    y3 = 11*q = y10;
    y4 = 7*q = y9;
    y18 = 5*q = y13;

    y5 = 9*q = y7 = y8;
    y6 = 10.5*q;
    y14 = 3*q = y16 = y17;
    y15 = 1.5*q;

    path p;
    p = reverse (z1{-1,1} .. z2{up} .. {1,1}z3{-1,-1}
        .. {right}z4{up} .. z5{up} .. z6{right} .. {down}z7{up} .. z8{down} .. {down}z9{right}
        .. {-1,1}z10{1,-1} .. z11{down} .. {-1,-1}z12{1,1}
        .. {left}z13{down} .. z14{down} .. z15{left} .. {up}z16{down} .. z17{up} .. {up}z18{left}
        .. {1,-1}cycle);

    pickup pencircle xscaled 0.03w yscaled 0.03w;
    penlabels(range 1 thru 18);
enddef;
beginchar(15, 1em#, 1em#, 0); "Dragon - mask";
    dragon_outline;
    fill p;
endchar;
beginchar(16, 1em#, 1em#, 0); "Dragon - white";
    dragon_outline;
    draw p;
endchar;

%%%%%%%%%%%%%%%%%%%% OLIPHANT %%%%%%%%%%%%%%%%%%%%
def oliphant_outline =
    numeric q;
    q = round 1/12em;

    x1 = 5.5*q = w - x13;
    x2 = 5*q = w - x12;
    x3 = 4.5*q = w - x11;
    x4 = 3.5*q = w - x10;
    x5 = 4*q = w - x9;
    x6 = 4.5*q = w - x8;
    x7 = 6*q;
    x14 = 4.5*q = w - x15;
    x16 = 4*q = w - x19;
    x17 = 1*q = w - x20;
    x18 = 2.5*q = w - x21;
    x22 = 3*q = w - x24;
    x23 = 4*q = w - x25;

    y1 = 1*q = y13;
    y2 = 4.5*q = y12;
    y3 = 5.5*q = y11;
    y4 = 8*q = y10;
    y5 = 10*q = y9;
    y6 = 10.5*q = y8;
    y7 = 11*q;
    y14 = 8.5*q = y15;
    y16 = 5*q = y19;
    y17 = 8*q = y20;
    y18 = 11*q = y21;
    y22 = 2.5*q = y24;
    y23 = 1*q = y25;

    path p;
    p = z1{up} .. z2 .. z3 .. z4 .. z5 .. z6 .. z7{right}
        .. z8 .. z9 .. z10 .. z11 .. z12 .. z13{down}
        -- cycle;
    path pel, per;
    pel = z16{-1,1} .. z17{up} .. z18{right} .. z6;
    per = z19{1,1} .. z20{up} .. z21{left} .. z8;
    path ptl, ptr;
    ptl = z3 .. z16 .. z22{down} .. z23{1,-1} & z23{-1,1} .. z2;
    ptr = z11 .. z19 .. z24{down} .. z25{-1,-1} & z25{1,1} .. z12;

    pickup pencircle xscaled 0.03w yscaled 0.03w;
    penlabels(range 1 thru 25);
enddef;
beginchar(18, 1em#, 1em#, 0); "Oliphant - mask";
    oliphant_outline;
    fill p;
    fill ptl & z2 -- cycle;
    fill reverse (ptr & z12 -- cycle);
    fill pel & z6 -- z5 -- z4 -- z3 -- cycle;
    fill per & z8 -- z9 -- z10 -- z11 -- cycle;
endchar;
beginchar(19, 1em#, 1em#, 0); "Oliphant - white";
    oliphant_outline;
    draw p;
    draw pel;
    draw per;
    draw ptl;
    draw ptr;
    pickup pencircle xscaled 0.08w yscaled 0.08w;
    drawdot z14;
    drawdot z15;
    pickup pencircle xscaled 0.03w yscaled 0.03w;
endchar;

%%%%%%%%%%%%%%%%%%%% KING %%%%%%%%%%%%%%%%%%%%
def king_outline =
    numeric q;
    q = round 1/12em;

    x1 = 3*q = w - x16;
    x2 = 3*q = w - x15;
    x3 = 3*q = w - x14;
    x4 = 1*q = w - x13;
    x5 = 2.5*q = w - x12;
    x6 = 4.5*q = w - x11;
    x7 = x8 = x9 = x10 = 6*q;
    x17 = x19 = 6*q;
    x18 = 5*q = w - x20;

    y1 = 1.5*q = y16;
    y2 = 2.5*q = y15;
    y3 = 3.5*q = y14;
    y4 = 8*q = y13;
    y5 = 9*q = y12;
    y6 = 8*q = y11;
    y7 = 4*q;
    y8 = 3*q;
    y9 = 2*q;
    y10 = 1*q;
    y18 = y20 = 10*q;
    y17 = 9*q;
    y19 = 11*q;

    path p;
    p = z1 -- z2 -- z3
        & z3 .. z4{up} .. z5{right} .. z6{1,-1} .. z7{down}
        & z7{up} .. z11{1,1} .. z12{right} .. z13{down} .. z14
        & z14 -- z15 -- z16
        & z16 .. z10{left} .. z1
        & cycle;
    path ph;
    ph = z6 .. z17{right} .. z11 -- z7 -- cycle;
    pickup pencircle xscaled 0.03w yscaled 0.03w;
    penlabels(range 1 thru 20);
enddef;
beginchar(21, 1em#, 1em#, 0); "King - mask";
    king_outline;
    fill p;
    fill ph;
endchar;
beginchar(22, 1em#, 1em#, 0); "King - white";
    king_outline;
    draw p;
    draw z6 .. z17{right} .. z11;
    draw z1 .. z9{right} .. z16;
    draw z2 .. z8{right} .. z15;
    draw z3 .. z7{right} .. z14;
    draw z18 -- z20; draw z17 -- z19;
endchar;

%%%%%%%%%%%%%%%%%%%% CLERIC %%%%%%%%%%%%%%%%%%%%
def cleric_outline =
    numeric q;
    q = round 1/12em;

    x1 = 1*q = w - x12;
    x2 = 1*q = w - x11;
    x3 = 6*q;
    x4 = 3*q = w - x10;
    x5 = 4*q = w - x9;
    x6 = 3*q = w - x8;
    x7 = 6*q;
    x13 = 6*q;
    x14 = 6*q;
    x15 = 6*q;
    x16 = 6*q;
    x17 = 5*q;
    x18 = 6*q;
    x19 = 7*q;

    y1 = 1*q = y12;
    y2 = 2*q = y11;
    y3 = 2.5*q;
    y4 = 3.5*q = y10;
    y5 = 5*q = y9;
    y6 = 7*q = y8;
    y7 = 10*q;
    y13 = 1.5*q;
    y14 = 4.25*q;
    y15 = 5.5*q;
    y16 = 6.5*q;
    y17 = 7.5*q;
    y18 = 8.5*q;
    y19 = 7.5*q;

    z7.u = z7 + ( 0.0*q,+1.0*q);
    z7.l = z7 + (-0.5*q,+0.5*q);
    z7.r = z7 + (+0.5*q,+0.5*q);

    path p;
    p = z1 -- z2 & z2{1,1} .. z3{1,1}
        & z3{left} .. z4{-1,1}
        & z4 -- z5
        & z5 .. z6{up} .. z7{1,1}
        & z7{1,-1} .. z8{down} .. z9
        & z9 -- z10
        & z10{-1,-1} .. z3{left}
        & z3{1,-1} .. z11{1,-1}
        & z11 -- z12
        & z12{-1,1} .. z13{-1,1}
        & z13{-1,-1} .. z1{-1,-1}
        & cycle;

    path po;
    po = z7{left} .. z7.l{up} .. z7.u{right} .. z7.r{down} .. cycle;

    pickup pencircle xscaled 0.03w yscaled 0.03w;
    penlabels(range 1 thru 19);
enddef;
beginchar(24, 1em#, 1em#, 0); "Cleric - mask";
    cleric_outline;
    fill p;
    fill po;
endchar;
beginchar(25, 1em#, 1em#, 0); "Cleric - white";
    cleric_outline;
    draw p;
    draw po;
    draw z4 .. z14{right} .. z10;
    draw z5 .. z15{right} .. z9;
    draw z16 -- z18;
    draw z17 -- z19;
endchar;

%%%%%%%%%%%%%%%%%%%% UNICORN %%%%%%%%%%%%%%%%%%%%
def unicorn_outline =
    numeric q;
    q = round 1/12em;

    x1 = 4*q;
    x2 = 6.5*q;
    x3 = 6.5*q;
    x4 = 3*q;
    x5 = 2.75*q;
    x6 = 2*q;
    x7 = 1.25*q;
    x8 = 2.5*q;
    x9 = 3*q;
    x10 = 3.9*q;
    x11 = 1.5*q;
    %x12 = 4.25*q;
    x13 = 4.5*q;
    x14 = 5*q;
    %x15 = 5*q;
    %x16 = 5.5*q;
    %x17 = 6.25*q;
    x18 = 6.25*q;
    x19 = 9*q;
    x20 = 10*q;
    x21 = 11*q;
    x22 = 10.5*q;
    x23 = 10.25*q;
    x24 = 9*q;
    x25 = 6.5*q;
    x26 = 2*q;
    x27 = 3.5*q;
    x28 = 4.25*q;
    x29 = 4*q;
    %x30 = 4*q;
    x31 = 3*q;

    y1 = 1*q;
    y2 = 6*q;
    y3 = 7.75*q;
    y4 = 3.8*q;
    y5 = 4*q;
    y6 = 4*q;
    y7 = 5*q;
    y8 = 7*q;
    y9 = 8.75*q;
    y10 = 9*q;
    y11 = 11*q;
    %y12 = 10.25*q;
    y13 = 10*q;
    y14 = 10*q;
    %y15 = 9.5*q;
    %y16 = 11*q;
    %y17 = 9.25*q;
    y18 = 10*q;
    y19 = 8.5*q;
    y20 = 7*q;
    y21 = 1*q;
    y22 = 1*q;
    y23 = 5*q;
    y24 = 8*q;
    y25 = 9.75*q;
    y26 = 5*q;
    y27 = 7.5*q;
    y28 = 8.5*q;
    y29 = 8*q;
    %y30 = 9.5*q;
    y31 = 4.5*q;

    path p;
    p = z1{up} .. z2{up}
        & z2{-1,-1} .. z4{-1,-1} & z4 .. z5{up}
        & z5 .. z7 .. z8 .. z9 .. {right}z10 .. {right}z14
        & z14 .. z18 .. z19 .. z20 .. z21{down}
        -- cycle;

    pickup pencircle xscaled 0.03w yscaled 0.03w;
    penlabels(range 1 thru 31);
enddef;
beginchar(27, 1em#, 1em#, 0); "Unicorn - mask";
    unicorn_outline;
    fill p;
    fill z10 -- z11 -- z14 .. {left} cycle;
endchar;
beginchar(28, 1em#, 1em#, 0); "Unicorn - white";
    unicorn_outline;
    draw p;
    draw z3 .. z2{-1,-1};
    draw z5 .. z31{up};
    draw z22 .. z23 .. z24 .. z25;
    draw z10 -- z11 -- z14;
    pickup pencircle xscaled 0.04w yscaled 0.08w rotated -30;
    drawdot z29;
    pickup pencircle xscaled 0.05w yscaled 0.06w rotated -20;
    drawdot z26;
endchar;


end;
